Unquantified {
	Program = Statement*
	Statement = (Assignment | Call) (eol | end)
	Assignment = variable "=" (Value | Parameters)
	Value = variable | dateTime | date | offset | timeFrame | symbol | numeric | keyword | stringValue | SymbolArray | Parameters
	Parameters = "{" whitespace* Parameter ("," whitespace* Parameter)* whitespace* "}"
	Parameter = identifier ":" (ValueRangeParameter | MultiValueParameter | stringParameter | bool)
	ValueRangeParameter = numeric ("to" numeric ("step" numeric)?)?
	MultiValueParameter = "[" numeric ("," numeric)* "]"
	Call = identifier Value ("," Value)*
	SymbolArray = "[" symbol SeparatedSymbol* "]"
	SeparatedSymbol = ("," | "|") symbol
	identifier = lower (alnum | "_") *
	variable = "$" identifier
	nonZeroDigit = "1".."9"
	numeric = "-"? nonZeroDigit digit* ("." digit+)?
	date = nonZeroDigit digit digit digit "-" digit digit "-" digit digit
	dateTime = date space+ digit digit ":" digit digit (":" digit digit)?
	offset = ("+" | "-") nonZeroDigit digit* ("mo" | "m" | "h" | "d" | "w" | "y")
	timeFrame = nonZeroDigit digit* ("m" | "h")
	symbol = (upper | nonZeroDigit) (upper | nonZeroDigit)+
	stringDelimiter = "\""
	string = stringDelimiter (~stringDelimiter any)* stringDelimiter
	stringValue = string
	stringParameter = string
	keyword = "true" | "false" | "first" | "last" | "now" | "daily" | "all"
	bool = "true" | "false"
	space := " " | "\t"
	whitespace = " " | "\t" | "\r" | "\n"
	eol = "\r"? "\n"
}